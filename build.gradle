plugins {
    id 'application'
    id 'distribution'
}

// disable start script. JAR file is runnable any way?
startScripts { enabled = false }

// disable default zip and tar distribution. They don't fit our preferred structure
distTar { enabled = false }
distZip { enabled = false }

// defined in settings.gradle
project.version = gradle.ext.version

// our custom zip dist.
// put jar, readme and release-info on top level
// put other documentation into docs/
task myDist(type: Zip){
    def baseDir = {archiveFileName.get() - '.zip'}
    into(baseDir){

        // include jar files from both sub projects
        // and make them executable
        from { project(':register').jar }
        fileMode = 0755
        from { project(':result').jar }
        fileMode = 0755

        // readme and release info
        from 'docs/README'
        from 'docs/config.yaml'
        from 'docs/start.bat'
        from 'docs/release-info.txt'
        from 'Web/package.json'
        from 'Web/package-lock.json'
        from 'Web/server/index.js'

        into("input"){
            from 'result/input'
        }


        into("output"){
            from 'result/output'
        }
        
        into("web") {
            from 'Web/build'
        }


        // all other documentation
        into("docs") {
            from 'docs/'
            exclude 'release-info.txt'
            exclude 'README'
            exclude 'config.yaml'
            exclude 'start.bat'
        }
    }
}

build {
    // make the build task also run our custom distribution
    finalizedBy(myDist)
}
